import discrete_valuation_ring.trivial_extension
import eq_characteristic.basic

/-!
# The canonical valuation on an equal characteristic local field

In this file we define the canonical valuation on an equal characteristic local field, which is the
`discrete_valuation.extended_valuation` constructed from the `X`-adic valuation on `ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯`.

## Main Definitions
* `eq_char_local_field.with_zero.valued` : the valued instance in an equal characteristic local
  field, induced by the extension of the `X`-adic valuation.

##  Main Theorems
* `eq_char_local_field.complete_space` : an equal characteristic local field is a complete space. 

* `eq_char_local_field.valuation.is_discrete` : the canonical valuation in an equal characteristic 
  local field is discrete.

* `eq_char_local_field.ring_of_integers.discrete_valuation_ring` : the ring of integers of an 
  equal characteristic local field is a discrete valuation ring. 

## Implementation details
Note that when `K = ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯`, there are two valued instances on it : the one coming from the fact 
that `ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯` is defined as the `adic_completion` of `ratfunc ğ”½_[p]` with respect to the ideal 
`(X)`, and the one obtained by extending this valuation on `ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯` to its trivial field extension
`ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯`. These instances are mathematically equivalent, but not definitionally equal. However,
the lemma `discrete_valuation.extension.trivial_extension_eq_valuation` from the file 
`discrete_valuation_ring.trivial_extension` allow us to easily translate between the two instances
on `ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯`, whenever this diamond comes up.

-/

noncomputable theory

open discrete_valuation is_dedekind_domain multiplicative nnreal polynomial ratfunc
  discrete_valuation.extension
open_locale eq_char_local_field nnreal discrete_valuation

namespace eq_char_local_field

variables (p : out_param (â„•)) [hp : fact (p.prime)] 

include hp
variables (K : Type*) [field K] [eq_char_local_field p K]

/-- The valued instance in an equal characteristic local field, induced by the extension of the 
  `X`-adic valuation.-/
@[priority 100] instance : valued K â„¤â‚˜â‚€ := extension.valued ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯ K

/-- An equal characteristic local field is a complete space. -/
@[priority 100] instance : complete_space K := extension.complete_space ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯ K

/-- The canonical valuation in an equal characteristic local field is discrete. -/
instance : valuation.is_discrete (eq_char_local_field.with_zero.valued p K).v := 
extension.is_discrete_of_finite ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯ K

/-- The ring of integers of an equal characteristic local field is a discrete valuation ring. -/
instance : discrete_valuation_ring (ğ“ p K) := 
integral_closure.discrete_valuation_ring_of_finite_extension ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯ K

variables {p}

lemma valuation_X_ne_zero : valued.v (algebra_map (ratfunc ğ”½_[p]) K X) â‰  (0 : â„¤â‚˜â‚€) :=
by simp only [ne.def, _root_.map_eq_zero, ratfunc.X_ne_zero, not_false_iff] 

/-- The ramification index of an equal characteristic local field `K` is given by the 
  additive valuation of the element `(X : K)`. -/
def ramification_index (K : Type*) [field K] [eq_char_local_field p K] : â„¤ := 
  -(with_zero.unzero (valuation_X_ne_zero K)).to_add

localized "notation (name := ramification_index)
  `e` := eq_char_local_field.ramification_index" in eq_char_local_field 

variables (p)

/-- The local field `ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯` is unramified. -/
lemma is_unramified_FpX_completion : e ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯ = 1 := 
begin
  have hX : (eq_char_local_field.with_zero.valued p (FpX_completion p)).v (FpX_completion.X p) = 
    (of_add (-1 : â„¤)),
  { have heq : (eq_char_local_field.with_zero.valued p (FpX_completion p)).v =
    extended_valuation (FpX_completion p) (FpX_completion p),
    { refl },
    rw [â† @FpX_completion.valuation_X p _, FpX_completion.X, FpX_int_completion.X,
      eq_char_local_field.with_zero.valued, heq,
      discrete_valuation.extension.trivial_extension_eq_valuation],
    refl },
  rw [ramification_index, neg_eq_iff_eq_neg, â† to_add_of_add (-1 : â„¤)],
  apply congr_arg,
  rw [â† with_zero.coe_inj, â† hX, with_zero.coe_unzero],
  refl,
end

/-- A ring equivalence between `ğ”½_[p]âŸ¦XâŸ§ and the valuation subring of `ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯` viewed as an equal
  characteristic local field. -/
noncomputable! def FpX_int_completion.equiv_valuation_subring : 
  ğ”½_[p]âŸ¦XâŸ§ â‰ƒ+* â†¥(eq_char_local_field.with_zero.valued p ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯).v.valuation_subring := 
{ to_fun    := Î» x,
  begin
    use x.1, 
    have heq : (eq_char_local_field.with_zero.valued p ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯).v x.val =
        extended_valuation ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯ ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯ x.val, { refl },
    rw [valuation.mem_valuation_subring_iff, heq, trivial_extension_eq_valuation ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯],
    exact x.2,
  end,
  inv_fun   := Î» x,
  begin
    use x.1,
    rw [FpX_int_completion, height_one_spectrum.mem_adic_completion_integers,
      â† trivial_extension_eq_valuation ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯],
    exact x.2
  end,
  left_inv  := Î» x, by simp only [subtype.val_eq_coe, set_like.eta],
  right_inv := Î» x, by simp only [subtype.val_eq_coe, set_like.eta],
  map_mul'  := Î» x y, by simp only [subtype.val_eq_coe, subring.coe_mul, mul_mem_class.mk_mul_mk],
  map_add'  := Î» x y, by simp only [subtype.val_eq_coe, subring.coe_add, add_mem_class.mk_add_mk] }

variable {p}

lemma FpX_int_completion.equiv_valuation_subring_comm :
  (algebra_map (eq_char_local_field.with_zero.valued p ğ”½_[p]âŸ®âŸ®XâŸ¯âŸ¯).v.valuation_subring K).comp 
    (FpX_int_completion.equiv_valuation_subring p).to_ring_hom = algebra_map ğ”½_[p]âŸ¦XâŸ§ K :=
rfl

end eq_char_local_field
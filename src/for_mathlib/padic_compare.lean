/-
Copyright (c) 2023 Mar√≠a In√©s de Frutos-Fern√°ndez, Filippo A. E. Nuccio. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mar√≠a In√©s de Frutos-Fern√°ndez, Filippo A. E. Nuccio
-/
import analysis.specific_limits.basic
import discrete_valuation_ring.complete
import number_theory.padics.padic_integers
import ring_theory.dedekind_domain.adic_valuation
import for_mathlib.ring_theory.dedekind_domain.ideal

--import for_mathlib.laurent_series_iso.old_power_series_adic_completion
--set_option profiler true

noncomputable theory

open is_dedekind_domain is_dedekind_domain.height_one_spectrum nnreal polynomial valuation 
  normalization_monoid multiplicative
open_locale nnreal discrete_valuation

def int.p_height_one_ideal (p : out_param ‚Ñï) [hp : fact (p.prime)] : 
  height_one_spectrum ‚Ñ§ :=
{ as_ideal := ideal.span{(p : ‚Ñ§)},
  is_prime := by { rw ideal.span_singleton_prime, 
    exacts [nat.prime_iff_prime_int.mp hp.1, nat.cast_ne_zero.mpr hp.1.ne_zero] },
  ne_bot   := by {simp only [ne.def, ideal.span_singleton_eq_bot, nat.cast_eq_zero],
    exact hp.1.ne_zero, }}
open int

open unique_factorization_monoid

open_locale classical

namespace padic_comparison

open padic

variables (p : out_param ‚Ñï) [fact (p.prime)]
  
local attribute [-instance] rat.metric_space rat.normed_field rat.densely_normed_field
  /- rat.normed_linear_ordered_field -/  rat.division_ring rat.normed_add_comm_group

instance : separated_space ‚Ñö_[p] := metric_space.to_separated

def padic_valued : valued ‚Ñö ‚Ñ§‚Çò‚ÇÄ := (p_height_one_ideal p).adic_valued

local attribute [instance] padic_valued

lemma padic_norm_of_int_eq_val_norm (x : ‚Ñ§) : ((padic_norm p x) : ‚Ñù) =
  with_zero_mult_int_to_nnreal (ne_zero.ne p) (valued.v (x : ‚Ñö)) := 
begin
  classical,
  by_cases hx : x = 0,
  { simp only [hx, padic_norm.zero, algebra_map.coe_zero, _root_.map_zero] },
  { have hx0 : ¬¨(x : ‚Ñö) = 0 := cast_ne_zero.mpr hx,
    have hv0 : valued.v (x : ‚Ñö) ‚â† (0 : ‚Ñ§‚Çò‚ÇÄ),
    { rw [ne.def, zero_iff], exact hx0 },
    have heq : multiplicative.of_add (-(associates.mk (p_height_one_ideal p).as_ideal).count 
      (associates.mk (ideal.span {x} : ideal ‚Ñ§)).factors : ‚Ñ§) = (with_zero.unzero hv0),
    { erw [‚Üê with_zero.coe_inj, ‚Üê int_valuation_def_if_neg _ hx, with_zero.coe_unzero, 
        valuation_of_algebra_map], refl, },
    have hx' : (ideal.span {x} : ideal ‚Ñ§) ‚â† 0,
    { rwa [ideal.zero_eq_bot, ne.def, ideal.span_singleton_eq_bot] },
    have hp : prime (p : ‚Ñ§),
    { exact nat.prime_iff_prime_int.mp _inst_1.1 },
    have hp' : (ideal.span {(p : ‚Ñ§)} : ideal ‚Ñ§).is_prime,
    { rwa ideal.span_singleton_prime (ne_zero.ne (p : ‚Ñ§)), },
    have hpne : (ideal.span {(p : ‚Ñ§)} : ideal ‚Ñ§) ‚â† ‚ä•,
    { rw [ne.def, ideal.span_singleton_eq_bot], exact ne_zero.ne (p : ‚Ñ§) }, 
    simp only [padic_norm.eq_zpow_of_nonzero hx0, with_zero_mult_int_to_nnreal, 
      with_zero_mult_int_to_nnreal_def, zero_iff,
      rat.cast_zpow, rat.cast_coe_nat, monoid_with_zero_hom.coe_mk, dif_neg hx0, coe_zpow, 
      nnreal.coe_nat_cast],
    apply congr_arg,
    rw [‚Üê heq, padic_val_rat.of_int_multiplicity (nat.prime.ne_one _inst_1.1) hx,
      to_add_of_add, neg_inj, nat.cast_inj, ‚Üê part_enat.coe_inj, part_enat.coe_get,
      multiplicity_eq_count_normalized_factors hp.irreducible hx, int.normalize_coe_nat,
      part_enat.coe_inj, count_normalized_factors_eq_count_normalized_factors_span hx (ne_zero.ne p) rfl hp, count_normalized_factors_eq_associates_count hx' hp' hpne],
    refl },
end

lemma padic_norm_eq_val_norm (z : ‚Ñö) : ((padic_norm p z) : ‚Ñù) =
  with_zero_mult_int_to_nnreal (ne_zero.ne p) (valued.v z) := 
begin
  by_cases hz : z = 0,
  { simp only [hz, padic_norm.zero, algebra_map.coe_zero, _root_.map_zero] },
  { obtain ‚ü®x, y, hxy‚ü© := is_localization.mk'_surjective (non_zero_divisors ‚Ñ§) z,
    have hz : is_localization.mk' ‚Ñö x y = x/y,
    { simp only [is_fraction_ring.mk'_eq_div, eq_int_cast, _root_.coe_coe] },
    erw [‚Üê hxy, valuation_of_mk', hz, padic_norm.div,_root_.coe_coe, rat.cast_div, map_div‚ÇÄ, 
      nonneg.coe_div],
    apply congr_arg2;
    { convert padic_norm_of_int_eq_val_norm p _, erw valuation_of_algebra_map }},
end

lemma uniform_inducing_coe : uniform_inducing (coe : ‚Ñö ‚Üí ‚Ñö_[p]) :=
begin
  have hp_one : (1 : ‚Ñù‚â•0) < p := nat.one_lt_cast.mpr (nat.prime.one_lt (fact.out _)),
  apply uniform_inducing.mk',
  simp_rw @metric.mem_uniformity_dist ‚Ñö_[p] _ _,
  refine (Œª S, ‚ü®Œª hS, _, _‚ü©),
  { obtain ‚ü®m, ‚ü®-, hM_sub‚ü©‚ü© := (valued.has_basis_uniformity ‚Ñö ‚Ñ§‚Çò‚ÇÄ).mem_iff.mp hS,
    set M := (with_zero_mult_int_to_nnreal (ne_zero.ne p) m.1).1 with hM,
    refine ‚ü®{p : ‚Ñö_[p] √ó ‚Ñö_[p] | dist p.1 p.2 < M}, ‚ü®‚ü®M, ‚ü®_, Œª a b h, h‚ü©‚ü©, Œª x y h, _‚ü©‚ü©,
    { exact with_zero_mult_int_to_nnreal_pos _ (is_unit_iff_ne_zero.mp (units.is_unit m)) },
    { apply hM_sub,
      simp only [set.mem_set_of_eq, dist] at h ‚ä¢,
      rwa [‚Üê padic.coe_sub, padic_norm_e.eq_padic_norm', padic_norm_eq_val_norm, hM,
        units.val_eq_coe, val_eq_coe, nnreal.coe_lt_coe,
        (with_zero_mult_int_to_nnreal_strict_mono hp_one).lt_iff_lt,
        ‚Üê neg_sub, valuation.map_neg] at h }},
  { rw (valued.has_basis_uniformity ‚Ñö ‚Ñ§‚Çò‚ÇÄ).mem_iff,
    rintros ‚ü®T, ‚ü®Œµ, ‚ü®hŒµ, H‚ü©‚ü©, h‚ü©,
    obtain ‚ü®M, hM‚ü© := (real.exists_strict_mono_lt (with_zero_mult_int_to_nnreal_strict_mono
      hp_one) hŒµ),
    { refine ‚ü®M, by triv, Œª q hq, _‚ü©,
      simp only [set.mem_set_of_eq, dist] at H hq,
      have : (‚Üëq.fst, ‚Üëq.snd) ‚àà T,
      { apply H,
        rw [‚Üê padic.coe_sub, padic_norm_e.eq_padic_norm', padic_norm_eq_val_norm, ‚Üê neg_sub,
          valuation.map_neg],
        exact (nnreal.coe_lt_coe.mpr
          ((with_zero_mult_int_to_nnreal_strict_mono hp_one).lt_iff_lt.mpr hq)).trans hM,},
      specialize h q.1 q.2 this,
      rwa prod.mk.eta at h }},
end

lemma dense_coe : dense_range  (coe : ‚Ñö ‚Üí ‚Ñö_[p]) := metric.dense_range_iff.mpr (padic.rat_dense p)

def padic_pkg : abstract_completion ‚Ñö :=
{ space            := ‚Ñö_[p],
  coe              := coe,
  uniform_struct   := infer_instance,
  complete         := infer_instance,
  separation       := infer_instance,
  uniform_inducing := uniform_inducing_coe p,
  dense            := dense_coe p }


def coe_ring_hom : ‚Ñö ‚Üí+* ‚Ñö_[p] :=
{ to_fun    := (padic_pkg p).2,
  map_one'  := rat.cast_one,
  map_mul'  := rat.cast_mul,
  map_zero' := rat.cast_zero,
  map_add'  := rat.cast_add }

namespace padic'

--`toDO`  do we really need to remove it?
-- local attribute [- instance] rat.cast_coe

@[reducible]
def Q_p : Type* := adic_completion ‚Ñö (p_height_one_ideal p)

instance : is_discrete (@valued.v (Q_p p) _ ‚Ñ§‚Çò‚ÇÄ _ _) := 
completion.is_discrete _ _ _

instance : normed_field (Q_p p) := rank_one_valuation.valued_field.to_normed_field (Q_p p) ‚Ñ§‚Çò‚ÇÄ

def padic'_pkg : abstract_completion ‚Ñö :=
{ space            := Q_p p,
  coe              := coe,
/-This `coe` is not the coercion from `‚Ñö` to every field of characteristic zero, but rather the
coercion from a space to its uniform completion-/
  uniform_struct   := infer_instance,
  complete         := infer_instance,
  separation       := infer_instance,
  uniform_inducing := (uniform_space.completion.uniform_embedding_coe ‚Ñö).1,
  dense            := uniform_space.completion.dense_range_coe }

end padic'

open padic'

def compare : Q_p p ‚âÉ·µ§ ‚Ñö_[p] :=
abstract_completion.compare_equiv (padic'_pkg p) (padic_pkg p)

/-`extension_as_ring_hom_to_fun` and its siblings might be redundant-/

lemma uniform_continuous_coe : uniform_continuous (coe : ‚Ñö ‚Üí ‚Ñö_[p]) :=
(uniform_inducing_iff'.1 (uniform_inducing_coe p)).1


definition extension_as_ring_hom : Q_p p ‚Üí+* ‚Ñö_[p] := 
uniform_space.completion.extension_hom (coe_ring_hom p) (uniform_continuous_coe p).continuous


@[simp]
lemma extension_as_ring_hom_to_fun : (extension_as_ring_hom p).to_fun =
  uniform_space.completion.extension (coe : ‚Ñö ‚Üí ‚Ñö_[p]) := rfl


lemma extension_eq_compare : (extension_as_ring_hom p).to_fun = (compare p).to_fun :=
begin
  simp only [extension_as_ring_hom_to_fun, equiv.to_fun_as_coe, uniform_equiv.coe_to_equiv],
  apply uniform_space.completion.extension_unique (uniform_continuous_coe p)
    ((padic'_pkg p).uniform_continuous_compare_equiv (padic_pkg p)),
  intro a,
  have : (padic_pkg p).coe a = (‚Üëa : ‚Ñö_[p]) := rfl,
  rw [‚Üê this, ‚Üê abstract_completion.compare_coe],
  refl,
end

definition padic_equiv : (Q_p p) ‚âÉ+* ‚Ñö_[p] :=
{ map_mul' := by {rw ‚Üê extension_eq_compare p, use (extension_as_ring_hom p).map_mul'},
  map_add' := by {rw ‚Üê extension_eq_compare p, exact (extension_as_ring_hom p).map_add'},
  ..(compare p) }

instance : char_zero (Q_p p) := (padic_equiv p).to_ring_hom.char_zero

@[reducible]
def Z_p := (@valued.v (Q_p p) _ ‚Ñ§‚Çò‚ÇÄ _ _).valuation_subring

lemma exists_mem_le_one_of_lt_one {x : (Q_p p)} (hx : valued.v x ‚â§ (1 : ‚Ñ§‚Çò‚ÇÄ)) : ‚àÉ (y : Z_p p),
  (y : (Q_p p)) = x ‚àß (valued.v (y : Q_p p)) = valued.v x :=
begin
  have hv := valued.v.is_equiv_valuation_valuation_subring,
  have := valuation_subring.mem_of_valuation_le_one (Z_p p) x _,
  use ‚ü®x, this‚ü©,
  simp only [set_like.coe_mk, eq_self_iff_true, and_self],
  exact ((valuation.is_equiv_iff_val_le_one _ _).mp hv).mp hx,
end

lemma exists_mem_lt_one_of_lt_one {x : (Q_p p)} (hx : valued.v x < (1 : ‚Ñ§‚Çò‚ÇÄ)) : ‚àÉ (y : Z_p p),
  (y : (Q_p p)) = x ‚àß (valued.v (y : Q_p p)) = valued.v x :=
begin
  have hv := valued.v.is_equiv_valuation_valuation_subring,
  have := valuation_subring.mem_of_valuation_le_one (Z_p p) x (le_of_lt _),
  use ‚ü®x, this‚ü©,
  simp only [set_like.coe_mk, eq_self_iff_true, and_self],
  exact ((valuation.is_equiv_iff_val_lt_one _ _).mp hv).mp hx,
end

-- TODO: slow
/-- TODO: possible diamond here (the proof for ‚Ñ§_[p] does not translate) -/
instance : char_zero (Z_p p) := 
{ cast_injective := Œª m n h, 
  begin
    simp only [subtype.ext_iff, subring.coe_nat_cast, nat.cast_inj] at h,
    exact h
  end }

def padic'_int.height_one_ideal (p : out_param ‚Ñï) [hp : fact (p.prime)] : 
  height_one_spectrum (Z_p p) :=
{ as_ideal := local_ring.maximal_ideal (Z_p p),
  is_prime := ideal.is_maximal.is_prime (local_ring.maximal_ideal.is_maximal _),
  ne_bot   := begin
    rw [ne.def, ‚Üê local_ring.is_field_iff_maximal_ideal_eq],
    exact discrete_valuation.not_is_field _
  end }

instance : valued (Q_p p) ‚Ñ§‚Çò‚ÇÄ := height_one_spectrum.valued_adic_completion ‚Ñö (p_height_one_ideal p)


/- The lemma `padic_int_ring_equiv_mem` states that an element `x ‚àà ‚Ñö_[p]` is in `‚Ñ§_[p]` if and
only if it is in the image of `Z_p p` via the ring equivalence `padic_equiv p`. See
`padic_int_ring_equiv` for an upgrade of this statement to a ring equivalence `Z_p p ‚âÉ+* ‚Ñ§_[p]`-/

def padic_int.valuation_subring : valuation_subring ‚Ñö_[p] :=
{ to_subring := padic_int.subring p,
  mem_or_inv_mem' :=
begin
  have not_field : ¬¨ is_field ‚Ñ§_[p] := (discrete_valuation_ring.not_is_field _),
-- Marking `not_field` as a separate assumption makes the computation faster
  have := ((discrete_valuation_ring.tfae ‚Ñ§_[p] not_field).out 0 1).mp
    padic_int.discrete_valuation_ring,
  intro x,
  rcases (valuation_ring.iff_is_integer_or_is_integer ‚Ñ§_[p] ‚Ñö_[p]).mp this x with hx | hx,
  { apply or.intro_left,
    obtain ‚ü®y, hy‚ü© := hx,
    rw ‚Üê hy,
    simp only [padic_int.algebra_map_apply, subring.mem_carrier, padic_int.mem_subring_iff,
      padic_int.padic_norm_e_of_padic_int],
    apply padic_int.norm_le_one },
  { apply or.intro_right,
    obtain ‚ü®y, hy‚ü© := hx,
    rw ‚Üê hy,
    simp only [padic_int.algebra_map_apply, subring.mem_carrier, padic_int.mem_subring_iff,
      padic_int.padic_norm_e_of_padic_int],
    apply padic_int.norm_le_one,
    },
end }
 
open filter
open_locale filter topology

@[reducible]
def comap_Zp : valuation_subring ‚Ñö_[p] :=
valuation_subring.comap (Z_p p) (padic_equiv p).symm.to_ring_hom

-- TODO: Ask on Zulip if it exists and move it to another file/folder
-- **FAE** This is in lean4 already
lemma nnreal.lt_one_of_tendsto_pow_0 (a : ‚Ñù‚â•0) (h : tendsto (Œª n : ‚Ñï, a^n) at_top (ùìù 0)) :
  a < 1 :=
begin
  by_cases ha‚ÇÄ : a = 0,
  {rw ha‚ÇÄ, exact zero_lt_one,},
  { by_contradiction ha_le,
    rw not_lt at ha_le,
    by_cases ha : a = 1,
    { simp only [ha, one_pow] at h,
      exact zero_ne_one (tendsto_nhds_unique h tendsto_const_nhds) },
    { replace h : tendsto (Œª n : ‚Ñï, (a : ennreal) ^n) at_top (ùìù 0),
      { rw ‚Üê ennreal.coe_zero,
        simp_rw [‚Üê ennreal.coe_pow, ennreal.tendsto_coe],
        exact h, },
      set b : ennreal := ‚Üë(a‚Åª¬π) with hb,
      replace h : tendsto (Œª n : ‚Ñï, b ^ n) at_top (ùìù ‚ä§),
      { rw [hb, ennreal.coe_inv ha‚ÇÄ],
        convert (@ennreal.tendsto_inv_iff ‚Ñï at_top (Œª n, (‚Üëa) ^ n) 0).mpr h,
        { funext n, exact ennreal.inv_pow.symm, },
        { simp only [ennreal.inv_zero] }},
      have hb‚ÇÅ : b < 1,
      { rw [hb, ‚Üê ennreal.coe_one, ennreal.coe_lt_coe],
        exact inv_lt_one (lt_of_le_of_ne ha_le (ne.symm ha)) },
      exact ennreal.zero_ne_top (tendsto_nhds_unique
        (ennreal.tendsto_pow_at_top_nhds_0_of_lt_1 hb‚ÇÅ) h)}},
  end


/-The two lemmas below have basically the same proof, except from the fact that in one we
 use that `x : ‚Ñö_[p]` satisfies ‚Äñ x ‚Äñ < 1 iff `p ‚à£ x` and in the other that `x : (Q_p p)` has
 ‚Äñ x ‚Äñ < 1 iff it belongs to the maximal ideal...-/
lemma padic_int.nonunit_mem_iff_top_nilpotent (x : ‚Ñö_[p]) :
  x ‚àà (padic_int.valuation_subring p).nonunits ‚Üî filter.tendsto (Œª n : ‚Ñï, x ^ n) at_top (ùìù 0) :=
begin
  have aux : ‚àÄ n : ‚Ñï, ‚Äñ x^n ‚Äñ = ‚Äñ x ‚Äñ ^ n,
  { exact Œª n, norm_pow _ n},
  rw [tendsto_zero_iff_norm_tendsto_zero, filter.tendsto_congr aux],
  refine ‚ü®Œª H, _, Œª H, _‚ü©,
  { obtain ‚ü®h1, h2‚ü© := valuation_subring.mem_nonunits_iff_exists_mem_maximal_ideal.mp H,
    exact _root_.tendsto_pow_at_top_nhds_0_of_lt_1 (norm_nonneg _)
       (padic_int.mem_nonunits.mp $ (local_ring.mem_maximal_ideal _).mp h2) },
  { have : ‚Äñ x ‚Äñ < 1,
    { suffices : (‚ü®‚Äñ x ‚Äñ, norm_nonneg _‚ü© : ‚Ñù‚â•0) < 1,
      { rwa [‚Üê nnreal.coe_lt_coe, nnreal.coe_one, ‚Üê subtype.val_eq_coe] at this },
      apply nnreal.lt_one_of_tendsto_pow_0,
      rwa [‚Üê nnreal.tendsto_coe, nnreal.coe_zero] },
    apply valuation_subring.mem_nonunits_iff_exists_mem_maximal_ideal.mpr,
    exact ‚ü®(padic_int.mem_subring_iff p).mpr (le_of_lt this), (local_ring.mem_maximal_ideal _).mpr
      (padic_int.mem_nonunits.mpr this)‚ü© },
end


--The lemma below could probably be incorporated in the proof of 
-- `unit_ball.nonunit_mem_iff_top_nilpotent` if the whole things does not become too long
lemma go_faster {x : (Q_p p)} (h_go : ‚Äñ x ‚Äñ < 1) (H : tendsto (Œª (n : ‚Ñï), ‚Äñx‚Äñ ^ n) at_top (ùìù 0)) :
  x ‚àà (Z_p p).nonunits :=
begin
  apply valuation_subring.mem_nonunits_iff_exists_mem_maximal_ideal.mpr,
  have : ‚Äñ x ‚Äñ < 1,
    { suffices : (‚ü®‚Äñ x ‚Äñ, norm_nonneg _‚ü© : ‚Ñù‚â•0) < 1,
      { rwa [‚Üê nnreal.coe_lt_coe, nnreal.coe_one, ‚Üê subtype.val_eq_coe] at this },
      apply nnreal.lt_one_of_tendsto_pow_0,
      rwa [‚Üê nnreal.tendsto_coe, nnreal.coe_zero] },
    replace this : valued.v x < (1 : ‚Ñ§‚Çò‚ÇÄ),
    { apply (rank_one_valuation.norm_lt_one_iff_val_lt_one x).mp this },
    obtain ‚ü®y, hy‚ÇÅ, hy‚ÇÇ‚ü© := exists_mem_lt_one_of_lt_one p this,
    rw [‚Üê hy‚ÇÇ] at this,
    have this' := this,
    rw [‚Üê completion.valuation.adic_of_compl_eq_compl_of_adic ‚Ñ§ (p_height_one_ideal p) ‚Ñö (‚Üëy)] at this,
    let M := (completion.max_ideal_of_completion) ‚Ñ§ (p_height_one_ideal p) ‚Ñö,
    have v_lt_one := @is_dedekind_domain.height_one_spectrum.valuation_lt_one_iff_dvd (Z_p p) _ _ _ (Q_p p)
      _ _ _ (completion.max_ideal_of_completion ‚Ñ§ (p_height_one_ideal p) ‚Ñö) y,
    have eq_y : (algebra_map ‚Ü•(Z_p p) (Q_p p)) y = (‚Üëy : (Q_p p)) := rfl,
    rw eq_y at v_lt_one,
    rw [v_lt_one] at this,
    simp only [ideal.dvd_span_singleton, mem_nonunits_iff, valuation_subring.algebra_map_apply,
      set_like.coe_mk, forall_true_left] at this,--squeeze_simp has a bizarre *unwanted* effect
    rw [‚Üê hy‚ÇÅ],
    simp only [mem_valuation_subring_iff, set_like.eta, exists_prop],
    exact ‚ü®le_of_lt this', this‚ü©,--this final `exact` probably shows that having both `this'` and `this` is perfectly useless
end

lemma unit_ball.nonunit_mem_iff_top_nilpotent (x : (Q_p p)) :
  x ‚àà (Z_p p).nonunits ‚Üî filter.tendsto (Œª n : ‚Ñï, x ^ n) at_top (ùìù 0) :=
begin
  have h_max_ideal : (padic'_int.height_one_ideal p).as_ideal =
    (local_ring.maximal_ideal ‚Ü•(Z_p p)) := rfl,
  have aux : ‚àÄ n : ‚Ñï, ‚Äñ x^n ‚Äñ = ‚Äñ x ‚Äñ ^ n,
  { exact Œª n, norm_pow _ n},
  rw [tendsto_zero_iff_norm_tendsto_zero, filter.tendsto_congr aux],
  refine ‚ü®Œª H, _, Œª H, _‚ü©,
  { simp_rw norm_pow,
    obtain ‚ü®h, x_mem‚ü© := valuation_subring.mem_nonunits_iff_exists_mem_maximal_ideal.mp H,
    have := (@valuation_lt_one_iff_dvd (Z_p p) _ _ _ (Q_p p) _ _ _ (padic'_int.height_one_ideal p)
      ‚ü®x, h‚ü©).mpr,
    simp only [h_max_ideal, ideal.dvd_span_singleton, mem_nonunits_iff,
      valuation_subring.algebra_map_apply, set_like.coe_mk, x_mem, forall_true_left] at this,
    replace this : valued.v x < (1 : ‚Ñ§‚Çò‚ÇÄ),
    { convert this using 1,
      exact (completion.valuation.adic_of_compl_eq_compl_of_adic ‚Ñ§
        (int.p_height_one_ideal p) ‚Ñö x).symm },
      exact _root_.tendsto_pow_at_top_nhds_0_of_lt_1 (norm_nonneg _)
      ((rank_one_valuation.norm_lt_one_iff_val_lt_one _ ).mpr this), },
  { have : ‚Äñ x ‚Äñ < 1,
    { suffices : (‚ü®‚Äñ x ‚Äñ, norm_nonneg _‚ü© : ‚Ñù‚â•0) < 1,
      { rwa [‚Üê nnreal.coe_lt_coe, nnreal.coe_one, ‚Üê subtype.val_eq_coe] at this },
      apply nnreal.lt_one_of_tendsto_pow_0,
      rwa [‚Üê nnreal.tendsto_coe, nnreal.coe_zero] },
      apply go_faster p this H }
end


lemma mem_nonunits_iff (x : (Q_p p)) :
  x ‚àà (Z_p p).nonunits ‚Üî (padic_equiv p) x ‚àà (comap_Zp p).nonunits :=
begin
  let œÜ : (Z_p p) ‚âÉ+* (comap_Zp p),
  { have :=  (Z_p p).to_subring.comap_equiv_eq_map_symm (padic_equiv p).symm,
    replace this := ring_equiv.subring_congr this.symm,
    use (@ring_equiv.subring_map _ _ _ _  (Z_p p).to_subring (padic_equiv p)).trans this, },
  refine ‚ü®Œª hx, _, Œª hx, _‚ü©,
  all_goals { rw valuation_subring.mem_nonunits_iff_exists_mem_maximal_ideal at hx,
    rw valuation_subring.mem_nonunits_iff_exists_mem_maximal_ideal},
    { refine ‚ü®_, map_nonunit (‚ÜëœÜ : ((Z_p p) ‚Üí+* (comap_Zp p))) _ hx.some_spec‚ü© },
    { rcases hx with ‚ü®h1, h2‚ü©,
      have h3 := valuation_subring.mem_comap.mp h1,
      have : ((padic_equiv p).symm.to_ring_hom) ((padic_equiv p) x) = 
        ((padic_equiv p).symm.to_ring_hom) ((padic_equiv p).to_ring_hom x) := rfl,
      simp_rw [this, ‚Üê ring_hom.comp_apply,
        ring_equiv.symm_to_ring_hom_comp_to_ring_hom, ring_hom.id_apply] at h3,
      have h4 : (œÜ.symm) (‚ü®(padic_equiv p) x, h1‚ü© : {z // z ‚àà comap_Zp p}) = ‚ü®x, h3‚ü©,
      { set b : ‚Ñö_[p] := œÜ ‚ü®x, h3‚ü© with hb,
        have : b = (padic_equiv p) x := rfl,
        simp_rw [‚Üê this, hb, set_like.eta, ring_equiv.symm_apply_apply], },
      replace h2 := map_nonunit (‚ÜëœÜ.symm : ((comap_Zp p) ‚Üí+* (Z_p p))) _ h2,
      erw h4 at h2,
      refine ‚ü®_, h2‚ü© },
end

lemma valuation_subrings_eq : padic_int.valuation_subring p = comap_Zp p :=
begin
  rw ‚Üê valuation_subring.nonunits_inj,
  ext x,
  refine ‚ü®Œª hx, _, Œª hx, _‚ü©,
  { rw [‚Üê (padic_equiv p).apply_symm_apply x], 
    rw [‚Üê mem_nonunits_iff, 
      unit_ball.nonunit_mem_iff_top_nilpotent, ‚Üê _root_.map_zero (padic_equiv p).symm],
    simp_rw [‚Üê _root_.map_pow (padic_equiv p).symm],
    apply (@continuous.continuous_at _ _ _ _ _ 0 (compare p).3.continuous).tendsto.comp,
    rwa ‚Üê padic_int.nonunit_mem_iff_top_nilpotent },
  { rw [‚Üê  (padic_equiv p).apply_symm_apply x, ‚Üê mem_nonunits_iff, 
      unit_ball.nonunit_mem_iff_top_nilpotent] at hx,
    replace hx := @tendsto.comp ‚Ñï (Q_p p) ‚Ñö_[p] (Œª n, ((padic_equiv p).symm x) ^ n) (padic_equiv p)
      at_top (ùìù 0) (ùìù 0) _ hx,
    -- We postpone the verification of the first assumption in `tendsto.comp`
    { simp_rw [‚Üê _root_.map_pow (padic_equiv p).symm x, function.comp, ring_equiv.apply_symm_apply]
      at hx,
      rwa padic_int.nonunit_mem_iff_top_nilpotent },
    { rw [‚Üê _root_.map_zero (padic_equiv p)],
      apply continuous.tendsto (compare p).symm.3.continuous 0}},
end

instance : algebra ‚Ñö_[p] (Q_p p) := 
ring_hom.to_algebra (padic_comparison.padic_equiv p).symm

instance : is_scalar_tower ‚Ñö ‚Ñö_[p] (Q_p p) := 
{ smul_assoc := Œª r x y, begin
    simp only [algebra.smul_def, eq_rat_cast, _root_.map_mul, map_rat_cast, mul_assoc],
    refl,
  end  }

lemma padic_valued_valuation_p : 
  @valued.v ‚Ñö _ ‚Ñ§‚Çò‚ÇÄ _ (padic_valued p) (p : ‚Ñö) = (of_add (-1 : ‚Ñ§)) := 
begin
  have hp : (p : ‚Ñö) = algebra_map ‚Ñ§ ‚Ñö (p : ‚Ñ§) := rfl,
  rw [adic_valued_apply, hp, valuation_of_algebra_map, fae_int_valuation_apply, 
    int_valuation_def_if_neg (p_height_one_ideal p) 
      (nat.cast_ne_zero.mpr (nat.prime.ne_zero _inst_1.1))],
  congr,
  apply associates.count_self,
  rw associates.irreducible_mk,
  apply prime.irreducible,
  exact ideal.prime_of_is_prime ( ideal.span_singleton_eq_bot.mp.mt (nat.cast_ne_zero.mpr 
    (nat.prime.ne_zero _inst_1.1))) (ideal.is_maximal.is_prime' (p_height_one_ideal p).as_ideal)
end

lemma padic'.coe_eq (x : ‚Ñö) : (x : Q_p p) = (((padic'_pkg p).coe x) : (padic'_pkg p).space) :=
begin
  have hp : (x : Q_p p) = (padic_pkg p).compare (padic'_pkg p) (x : ‚Ñö_[p]),
  { have h: (padic_pkg p).compare (padic'_pkg p) (x : ‚Ñö_[p]) = algebra_map ‚Ñö_[p] (Q_p p) x := rfl,
    rw [h, map_rat_cast] },
  rw [‚Üê abstract_completion.compare_coe (padic_pkg p) (padic'_pkg p), hp],
  refl,
end

lemma padic'.valuation_p : 
   valued.v (p : Q_p p) = (of_add (-1 : ‚Ñ§)) := 
begin
   letI : valued ‚Ñö ‚Ñ§‚Çò‚ÇÄ := padic_valued p,
   have hp : (p : Q_p p) = (((coe : ‚Ñö ‚Üí (Q_p p)) p) : Q_p p),
   { have : ‚àÄ x : ‚Ñö, (coe : ‚Ñö ‚Üí (Q_p p)) x = (x : Q_p p),
     { intro x, rw padic'.coe_eq, refl, },
     rw this, simp only [rat.cast_coe_nat], },
   rw [hp, valued.valued_completion_apply (p : ‚Ñö), padic_valued_valuation_p p],
 end

lemma padic'_int.height_one_ideal_def : 
  (padic'_int.height_one_ideal p).as_ideal = ideal.span {(p : Z_p p)} := 
discrete_valuation.is_uniformizer_is_generator _ (padic'.valuation_p p)

--TODO: Golf proof!
lemma padic_int_ring_equiv_range :
  (Z_p p).map (padic_equiv p).to_ring_hom = padic_int.subring p :=
begin
  have : (comap_Zp p).to_subring = (padic_int.valuation_subring p).to_subring,
  rw ‚Üê valuation_subrings_eq,
  convert this,
  ext x,
  -- rw comap_Zp,
  -- rw ‚Üê subring.mem_carrier,
  -- rw ‚Üê subring.mem_carrier,
  simp only [subring.mem_carrier, subring.mem_map, --valuation_subring.mem_to_subring, 
  mem_valuation_subring_iff, 
  exists_prop, valuation_subring.mem_comap],
  split,
  { rintros ‚ü®y, ‚ü®hy, H‚ü©‚ü©,
    rw ‚Üê H,
    simp only [valuation_subring.mem_to_subring, valuation_subring.mem_comap,
      ring_equiv.symm_to_ring_hom_apply_to_ring_hom_apply,
    mem_valuation_subring_iff] at hy ‚ä¢,
    exact hy, },
  { intro hx,
    simp at hx,
    use (padic_equiv p).symm.to_ring_hom x,
    split,
    { simp only [valuation_subring.mem_to_subring, mem_valuation_subring_iff],
      exact hx },
    simp only [ring_equiv.to_ring_hom_apply_symm_to_ring_hom_apply],  },
end

noncomputable!
definition padic_int_ring_equiv :  (Z_p p) ‚âÉ+* ‚Ñ§_[p] :=
(ring_equiv.subring_map _).trans (ring_equiv.subring_congr (padic_int_ring_equiv_range p))



end padic_comparison